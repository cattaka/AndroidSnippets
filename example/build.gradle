apply plugin: 'com.android.application'

def enable_google_services = project.file("./google-services.json").exists()
if (enable_google_services) {
    apply plugin: 'com.google.gms.google-services'
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "net.cattaka.android.snippets.example"

        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(project.APP_VERSION_CODE)
        versionName project.APP_VERSION_NAME

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "boolean", "ENABLE_GOOGLE_SERVICES", String.valueOf(enable_google_services)
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled true
    }
}

repositories {
    maven {
        url "http://dl.bintray.com/cattaka/maven"
    }
}

def play_services = '11.4.2'
def support_library = project.SUPPORT_LIBRARY_VERSION

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':snippets')
    implementation "com.android.support:appcompat-v7:$support_library"
    implementation "com.android.support:cardview-v7:$support_library"
    implementation "com.android.support:recyclerview-v7:$support_library"
    implementation "com.android.support:design:$support_library"

    implementation "com.google.firebase:firebase-core:$play_services"

    implementation 'net.cattaka:adapter-toolbox:0.6.0@aar'
    implementation 'net.cattaka:cathandsgendroid-core:0.5.1'
    annotationProcessor 'net.cattaka:cathandsgendroid-apt:0.5.1'

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation 'com.google.dagger:dagger-android:2.11'
    implementation 'com.google.dagger:dagger-android-support:2.11'
    annotationProcessor "com.google.dagger:dagger-compiler:2.11"
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.11'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.trello:rxlifecycle:0.6.1'
    implementation 'com.trello:rxlifecycle-components:0.6.1'

    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.4.2'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test:runner:0.5') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestImplementation('com.android.support.test:rules:0.5') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'org.mockito:mockito-core:1.9.5'

    testImplementation 'junit:junit:4.12'
}
